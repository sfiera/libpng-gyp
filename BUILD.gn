declare_args() {
  if (target_os == "linux" && rebase_path(".", "//") != ".") {
    libpng = "system"
  } else {
    libpng = "bundled"
  }
}
assert(libpng == "system" || libpng == "bundled")

if (libpng == "bundled") {
  config("libpng_private") {
    if (current_toolchain != "//build/lib/win:msvc") {
      cflags = [
        "-Wall",
        "-Werror",
      ]
    }
    include_dirs = [
      "libpng-1.6.38",
      "src",
    ]
  }

  config("libpng_public") {
    include_dirs = [ "include" ]
    if (current_toolchain != "//build/lib/win:msvc") {
      libs = [ "m" ]
    }
  }

  static_library("libpng") {
    sources = [
      "libpng-1.6.38/arm/arm_init.c",
      "libpng-1.6.38/arm/filter_neon_intrinsics.c",
      "libpng-1.6.38/png.c",
      "libpng-1.6.38/pngerror.c",
      "libpng-1.6.38/pngget.c",
      "libpng-1.6.38/pngmem.c",
      "libpng-1.6.38/pngpread.c",
      "libpng-1.6.38/pngread.c",
      "libpng-1.6.38/pngrio.c",
      "libpng-1.6.38/pngrtran.c",
      "libpng-1.6.38/pngrutil.c",
      "libpng-1.6.38/pngset.c",
      "libpng-1.6.38/pngtrans.c",
      "libpng-1.6.38/pngwio.c",
      "libpng-1.6.38/pngwrite.c",
      "libpng-1.6.38/pngwtran.c",
      "libpng-1.6.38/pngwutil.c",
    ]
    if (target_os == "mac") {
      sources += [ "libpng-1.6.38/arm/palette_neon_intrinsics.c" ]
    }
    deps = [ "//ext/zlib" ]
    configs += [ ":libpng_private" ]
    public_configs = [ ":libpng_public" ]
  }

  executable("pngtest") {
    sources = [ "libpng-1.6.38/pngtest.c" ]
    deps = [ ":libpng" ]
  }
} else {
  import("//build/lib/pkg_config.gni")

  pkg_config("libpng_public") {
    lib = "libpng"
  }

  source_set("libpng") {
    configs += [ ":libpng_public" ]
  }
}
